uniform 0 Matrix4 uniform_ModelViewMatrix;
uniform 1 Matrix4 uniform_ModelViewProjectionMatrix;
uniform 2 Matrix3 uniform_NormalMatrix;
uniform 3 Texture2 uniform_Texture;
uniform 4 Vector3 uniform_FogColor;
uniform 5 Float uniform_FogEnd;
uniform 6 Texture2 uniform_BackgroundTexture;
uniform 7 Float uniform_Time;
uniform 8 Float uniform_Sunlight;
uniform 9 Vector3 uniform_SunlightNormal;
uniform 10 Float uniform_CamLight;

const Vector3 BLOCKLIGHT_NORMAL = vector3(0.3, 0.1, 0.6);
const Vector2 SCREEN_SIZE = vector2(float($SCENE_WIDTH), float($SCENE_HEIGHT));

shader vertex(0 Vector4 attribute_Position,
              1 Vector4 attribute_Color,
              2 Vector2 attribute_Texture,
              3 Vector3 attribute_Normal,
              4 Vector2 attribute_Light) {
    varying_Texture = attribute_Texture;
    varying_Color = attribute_Color;
    Vector4 vertex = attribute_Position;
    varying_Depth = length(vector3(uniform_ModelViewMatrix * vertex));
    Float lightSunLevel = max(attribute_Light.y - uniform_Sunlight, 0.0);
    Vector3 lightSun = vector3(lightSunLevel);
    Vector3 lightBlock = vector3(attribute_Light.x);
    lightBlock *= 0.5;
    lightBlock.gb *= lightBlock.gb;
    lightBlock.b *= lightBlock.b;
    if (varying_Depth < 32.0 * uniform_CamLight) {
        Vector3 realCamLight = vector3(clamp((1.0 - varying_Depth * 0.04) * uniform_CamLight, 0.0, 1.0));
        realCamLight.gb *= realCamLight.gb;
        realCamLight.b *= realCamLight.b;
        varying_Color.rgb *= max(max(lightSun, lightBlock), realCamLight);
    } else {
        varying_Color.rgb *= max(lightSun, lightBlock);
    }
    Vector3 normal = uniform_NormalMatrix * attribute_Normal;
    Float light = (mix(dot(normal, BLOCKLIGHT_NORMAL), dot(normal, uniform_SunlightNormal), lightSunLevel) + 1.2) / 2.2;
    varying_Color.rgb *= light;
    out_Position = uniform_ModelViewProjectionMatrix * vertex;
}

shader fragment(Vector4 varying_Color,
                Vector2 varying_Texture,
                Float varying_Depth) {
    out_Color = texture(uniform_Texture, varying_Texture);
    out_Color.a *= varying_Color.a;
    if (out_Color.a <= 0.01) {
        discard;
    }
    Float fog = min(varying_Depth / uniform_FogEnd, 1.0);
    Float fogBackground = mix(10.0, 0.0, fog);
    if (fogBackground <= 0.01) {
        discard;
    }
    fog *= fog;
    out_Color.rgb = mix(out_Color.rgb * varying_Color.rgb, uniform_FogColor, fog);
    if (fogBackground < 1.0) {
        Vector3 background = texture(uniform_BackgroundTexture, varying_Fragment.xy / SCREEN_SIZE).rgb;
        out_Color.rgb = mix(background, out_Color.rgb, fogBackground);
    }
}

outputs(0 Vector4 out_Color);